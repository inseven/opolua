DECLARE EXTERNAL
INCLUDE "const.oph"

EXTERNAL loop%:

PROC main:
	GLOBAL choice%
	LOCAL cont%
	cont% = 1
	WHILE cont%
		cont% = loop%:
	ENDWH
ENDP

PROC buttonDlg:
	LOCAL result%
	dINIT "Buttons"
	dBUTTONS "OK", 13, "Cancel", -27, "Tab", 9, "A", %a, "B", -%b
	result% = DIALOG
	IF result% = 13:
		PRINT "OK"
	ELSEIF result% = 9
		PRINT "Tab"
	ELSEIF result% = %a
		PRINT "A"
	ELSEIF result% = %b
		PRINT "B"
	ELSEIF result% = 0
		PRINT "Dialog Cancelled (result = 0)"
	ENDIF
	GET
ENDP

PROC bareButtonDlg:
	LOCAL result%
	dINIT "Buttons"
	dBUTTONS "No", $34E, "Yes", $359
	result% = DIALOG
	PRINT "Dialog returned", result%
	GET
ENDP

PROC sideButtonsDlg:
	LOCAL x%
	LOCAL result%
	dINIT "Side buttons", KDlgButRight% REM OR KDlgFillScreen%
	dBUTTONS "Single", %S, "Multi" + CHR$(10) + "Line", %M
	dTEXT "", "Some text"
	result% = DIALOG
	PRINT "Dialog returned", result%
	GET
ENDP


PROC longDlg:
	LOCAL val&
	dINIT "Long input"
	dLONG val&, "Long", -1, 50
	dTEXT "Another", "Option", KDTextAllowSelection%
	dBUTTONS "OK", 13
	DIALOG
	PRINT "Entered:", val&
	GET
ENDP

PROC floatDlg:
	LOCAL fval
	fval = 2.0
	dINIT "Float input"
	dFLOAT fval, "Float", -1.1, 5.0
	dTEXT "Another", "Option", KDTextAllowSelection%
	dBUTTONS "OK", 13
	DIALOG
	PRINT "Entered:", fval
	GET
ENDP

PROC editDlg:
	LOCAL val$(16)
	val$ = "Hello"
	dINIT "Text edit"
	dEDIT val$, "Text"
	dTEXT "Another", "Option", KDTextAllowSelection%
	dBUTTONS "OK", 13
	DIALOG
	PRINT "Got >"; val$; "<"
	GET
ENDP

PROC checkDlg:
	LOCAL chk%
	dINIT "Checkbox"
	dCHECKBOX chk%, "check"
	DIALOG
	PRINT "check is", chk%
	GET
ENDP

PROC textDlg:
	LOCAL x%
	dINIT "Text test"
	dTEXT "", "Right aligned linebelow", $601
	dTEXT "", "Full width text that isn't selectable"
	dTEXT "", "", $800
	dTEXT "Prompt", "Body text", $600
	dTEXT "Another", "Option", $400
	x% = DIALOG
	PRINT "Selected", x%
	GET
ENDP

PROC xDlg:
	LOCAL x$(20), ret%
	x$ = "12345"
	dINIT "Password test"
	dXINPUT x$, "Pass"
	ret% = DIALOG
	PRINT "Password test returned", ret%, "val:", x$
	GET
ENDP

PROC emptyDlg:
	dINIT "No items dialog"
	PRINT "No items dialog returned", DIALOG
	GET
ENDP

PROC untitledDlg:
	LOCAL ret%
	dINIT
	dTEXT "", "Unselectable text, no title"
	ret% = DIALOG
	PRINT "No title dialog returned", ret%

	dINIT ""
	dTEXT "", "Unselectable text, empty title"
	ret% = DIALOG
	PRINT "Empty title dialog returned", ret%

	dINIT "Nope", KDlgNoTitle%
	dTEXT "", "Unselectable text, titleless"
	ret% = DIALOG
	PRINT "Titleless dialog returned", ret%
ENDP

PROC dateDlg:
	LOCAL x&, ret%
	x& = DAYS(DAY, MONTH, YEAR)
	dINIT "Date test"
	dDATE x&, "Date", x& - 1, x& + 6
	dTEXT "Another", "Option", KDTextAllowSelection%
	ret% = DIALOG
	PRINT "Date test returned", ret%, "val:", x&
	GET
ENDP

PROC timeDlg:
	LOCAL thms&, thm&, thms24&, dhm&, dhms&, dms&
	thms& = HOUR * INT(3600) + MINUTE * 60 + SECOND
	thm& = thms&
	thms24& = thms&
	dhm& = 65
	dhms& = 65
	dms& = 65
	dINIT "Time test"
	dTIME thm&, "Time H:M", 0, 0, &1517F
	dTIME thms&, "Time H:M:S", 1, 0, &1517F
	dTIME thms24&, "Time H:M:S 24", 9, 0, &1517F
	dTIME dhm&, "Duration H:M", 2, 0, 600
	dTIME dhms&, "Duration H:M:S", 3, 0, 600
	dTIME dms&, "Duration M:S", 7, 0, 6000
	DIALOG
	PRINT "Entered:", thm&, thms&, thms24&, dhm&, dhms&, dms&
	GET
ENDP

PROC cancelDlg:
	LOCAL result%
	dINIT "Doom"
	dTEXT "", "EVERYTHING IS DOOMED"
	dBUTTONS "Sob", 27
	result% = DIALOG
	PRINT "Dialog returned", result%
	GET
ENDP

PROC cancelWithChoiceDlg:
	LOCAL result%, choice%
	dINIT "Doom"
	dTEXT "", "EVERYTHING IS DOOMED"
	dCHOICE choice%, "Doom", "Doom,Doom,Doom"
	dBUTTONS "Sob", -(%S + 512), "Reject reality", %R + 512
	result% = DIALOG
	PRINT "Dialog returned", result%, "choice=", choice%
	GET
ENDP

PROC editMultiDlg:
	LOCAL result%, buf&(16), ptr&, text$(60), len&, i%
	i% = 0
	WHILE i% < 20
		text$ = text$ + NUM$(i%, 2) + CHR$(7)
		i% = i% + 1
	ENDWH

	ptr& = ADDR(buf&())
	POKE$ ptr& + 3, text$
	POKEL ptr&, LEN(text$)

	dINIT "Multi edit"
	dEDITMULTI ptr&, "Prompt", 20, 5, 60
	dTEXT "Another", "Option", KDTextAllowSelection%
	dBUTTONS "OK", %o
	result% = DIALOG
	len& = PEEKL(ptr&)
	POKEB ptr& + 3, len&
	text$ = PEEK$(ptr& + 3)
	PRINT "Dialog returned", text$
	GET
ENDP

PROC fileDlg:
	LOCAL f$(255), type%, showz%, onlydir%, allowEmpty%
	LOCAL result%, flags%
	result% = 1
	allowEmpty% = 1
	type% = 1

	WHILE result%
		flags% = 0
		IF allowEmpty%
			flags% = flags% OR KDFileAllowNullStrings%
		ENDIF
		IF type% = 1
			REM open file
		ELSEIF type% = 2
			REM save file (+ query)
			flags% = flags% OR KDFileEditBox% OR KDFileEditorQueryExisting%
		ELSEIF type% = 3
			REM save file (-query)
			flags% = flags% OR KDFileEditBox%
		ELSEIF type% = 4
			REM New file
			flags% = flags% OR KDFileEditBox% OR KDFileEditorDisallowExisting%
		ENDIF
		IF showz%
			flags% = flags% OR KDFileSelectorWithRom%
		ENDIF
		IF onlydir%
			REM these two flags appear too broken for me to figure out what they should do.
			REM flags% = flags% OR KDFileAllowFolders% OR KDFileFoldersOnly%
		ENDIF

		dINIT "File dialog", KDlgButRight% OR KDlgDensePack%
		dFILE f$, "File,Dir,Drive", flags%
		REM dCHECKBOX edit%, "Allow text entry"
		dCHOICE type%, "Type", "Open file,Save file (query),Save file (overwrite),New file"
		dCHECKBOX showz%, "Show Z:"
		REM dCHECKBOX onlydir%, "Only directories"
		dCHECKBOX allowEmpty%, "Allow empty"
		dBUTTONS "OK", KDButtonEnter% + KDButtonNoLabel%, "Cancel", -(KDButtonEsc% + KDButtonNoLabel%)
		result% = DIALOG

		CLS
		PRINT "Result is", result%, f$
	ENDWH

	GET
ENDP

PROC emptyChoiceDlg:
	LOCAL ch%
	dINIT "Empty choice list dialog"
	dCHOICE ch%, "Choose", ""
	PRINT DIALOG
	GET
ENDP


PROC loop%:
	LOCAL dlg%
	EXTERNAL choice%
	dINIT "Dialog test", 0
	dTEXT "", "Pick a dialog type to test"
	REM dTEXT "", "", $800
	dCHOICE choice%, "Choose", "long,float,edit,checkbox,text,..."
	dCHOICE choice%, "", "password,empty,date,time,..."
	dCHOICE choice%, "", "buttons,bareButtons,sideButtons,cancelDlg,cancelWithChoiceDlg,..."
	dCHOICE choice%, "", "editmulti,file,emptychoice,untitled"
	dBUTTONS "OK", 13, "Cancel", -(512 + %q)
	choice% = 2
	dlg% = DIALOG
	PRINT "Dialog returned ", dlg%
	PRINT "Choice was ", choice%
	IF dlg% = 0
		PRINT "(dialog cancelled)"
		GET
		RETURN 0
	ELSEIF choice% = 1
		longDlg:
	ELSEIF choice% = 2
		floatDlg:
	ELSEIF choice% = 3
		editDlg:
	ELSEIF choice% = 4
		checkDlg:
	ELSEIF choice% = 5
		textDlg:
	ELSEIF choice% = 6
		xDlg:
	ELSEIF choice% = 7
		emptyDlg:
	ELSEIF choice% = 8
		dateDlg:
	ELSEIF choice% = 9
		timeDlg:
	ELSEIF choice% = 10
		buttonDlg:
	ELSEIF choice% = 11
		bareButtonDlg:
	ELSEIF choice% = 12
		sideButtonsDlg:
	ELSEIF choice% = 13
		cancelDlg:
	ELSEIF choice% = 14
		cancelWithChoiceDlg:
	ELSEIF choice% = 15
		editMultiDlg:
	ELSEIF choice% = 16
		fileDlg:
	ELSEIF choice% = 17
		emptyChoiceDlg:
	ELSEIF choice% = 18
		untitledDlg:
	ENDIF
	
	RETURN 1
ENDP
